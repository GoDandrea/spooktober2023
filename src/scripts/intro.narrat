main:
  // Just setup, no dialogue
  empty_sprites
  clear_dialog
  play music ambience_1
  play sound ambience_2
  change_theme default
  set_screen mother_house 0 fade 2000 2000

  run setup_protag

  jump sceneOneIntroduction

sceneOneIntroduction:
  // Dialogue begins
  talk mother idle "So, that's how it is?"
  run as_protag "Look, mom..."
  
  choice:
    talk mother idle "You'll take your things and leave?"
    "...": 
      run as_protag "..."
    "Yeah":
      run as_protag "Yeah. Yes I will."
    "It's not like that":
      run as_protag "It's not like that, mom."

  talk mother idle "Just like that, huh? Out of the blue."
  talk mother idle "You've barely landed a new job and already decided to pack it up."
  
  choice: 
    talk mother idle "How long have you been planning this?"
    "It's none of your business": // Reject; "I won't justify myself"
      run as_protag "It's none of your business."
    "A few weeks": // Accept; Answer straight
      run as_protag "A few weeks."
    "A few years": // Deflect; "I've always wanted to move away"
      run as_protag "A few years."

  talk mother idle "And was a little \"goodbye\" ever in those plans?"
  talk mother idle "Maybe a \"thanks for what you've done\"?"
  run as_protag "..."
  talk mother idle "Sure. Ok."
  talk mother idle "What about telling me?"

  choice:
    talk mother idle "When were you going to tell me you're leaving for good?"
    "I didn't know how you'd take it":
      run as_protag "I didn't know how you'd take it."
      run as_protag "How you'd react to the news."
      talk mother idle "How noble of you. To spare me the heartbreak."
    "I don't owe you an explanation":
      run as_protag "I don't owe you an explanation."
    "I wasn't":
      run as_protag "I wasn't."

  talk mother idle "Just leave me hurting by myself, then?"
  talk mother idle "Such a big part of my life."
  talk mother idle "So much work."
  talk mother idle "So much <b>love</b>."
  talk mother idle "Walking out the door like this, out of nowhere."
  run as_protag "It's not out of nowhere mom. Please."
  run as_protag "I'm an adult. I work. I can afford a place."
  talk mother idle "Barely! You barely make enough."
  talk mother idle "You barely have any money saved"
  run as_protag "Enough means enough, mom."
  run as_protag "I can afford it."
  talk mother idle "..."
  run as_protag "..."
  talk mother idle "...it's because of me, isn't it?"
  talk mother idle "You don't want your own place."
  run as_protag "Mom,"
  talk mother idle "The responsibilities. The sheer cost!"
  run as_protag "Mom! What are you on about?"
  talk mother idle "You don't want your place."

  choice:
    talk mother idle "You want to get away from me."
    "Why would I not want my own place?":
      run as_protag "Why would I now want my own place?"
    "I do":
      run as_protag "I do. I want space."
    "This is <b>not</b> about you!":
      run as_protag "This is <b>not</b> about you!"
  
  talk mother idle "Why do all of this?"
  talk mother idle "Why give up on food, shelter, clean clothes?"
  talk mother idle "On your own mother?"
  talk mother idle "On your own mother who <i>needs</i> you!"
  talk mother idle "Who <i>loves</i> you!"
  run as_protag "Mom, that's enough!"
  talk mother idle "Just to hurt me???"
  run as_protag "That's not...!"
  talk mother idle "Why, Mona? Why do this to me?"
  talk mother idle "Why <i>be</i> like this, Mona???"
  run as_protag "Mother, stop! That's not how it works!"
  talk mother idle "You know what? I've had enough."
  talk mother idle "You're just like him."
  
  choice:
    talk mother idle "Your <i>father</i>."
    "No. I'm like you.":
      run as_protag "No. I'm like you."
      run as_protag "And just like you, I want things my way."
      run as_protag "<b>My</b> things."
    "Stop it! I just want some space.":
      run as_protag "Stop it! I just want some space."
      run as_protag "You're suffocating me!"
      run as_protag "..."
    "I am. That's why I'm also leaving you.":
      run as_protag "Yes I am. Just like him."
      run as_protag "Which is why I'm also leaving you behind."

  run as_protag "Goodbye."
  jump callExStart